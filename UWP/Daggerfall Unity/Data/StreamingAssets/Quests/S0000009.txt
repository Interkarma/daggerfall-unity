-- Quest: C:\QUESTS\WORKING\S0000009.Qbn.
-- Main story quest.
-- QuestId: 9
Messages: 22
Quest: S0000009
DisplayName: Elysana's Betrayal
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                  You are even more dashing in person
<ce>                 than Lord Woodborne said. I just love
<ce>                  talking to men and women of action!
<ce>                    I know that you're looking for a
<ce>                   certain object, %pcf. A totem pole
<ce>                  or something? Well, an acquaintance
<ce>                    of mine, a perfectly delightful
<ce>                        =contact_, has something
<ce>                    that may help you. If you would
<ce>                       escort my cousin _foil_ to
<ce>                       _contact_ you can have it.
<ce>                    Would you do this teensy weensy
<ce>                             favor for me?

RefuseQuest:  [1001]
<ce>                   No? How dreadful! My poor cousin
<ce>                    will be so disappointed. You are
<ce>                  not nearly as fun as Lord Woodborne
<ce>                           said you would be.

AcceptQuest:  [1002]
<ce>                    How exciting! It will be a real
<ce>                    adventure. _foil_ just loves to
<ce>                travel and has been dying to spend some
<ce>                       time with _contact_. Go to
<ce>                      ___contact_ of ____contact_,
<ce>                     to a place called __contact_.
<ce>                %g will pay you =reward_ gold pieces for
<ce>                     bringing %g2 _foil_. Don't you
<ce>                  just love these dangerous missions?

QuestFail:  [1003]
<ce>                  I have nothing to discuss with you.

QuestComplete:  [1004]
<ce>                      %pcf! How did...I mean, I'm
<ce>                      glad to see you. And _foil_
<ce>                too. Here take the gold. Object? What's
<ce>                a totem? Well, its been nice talking to
<ce>                            you. Gotta run!

RumorsDuringQuest:  [1005]
A crowd of shady characters was here a while ago, looking for a %ra mercenary.
<--->
Princess Elysana been spending a lot of time in the Wayrest wilderness, I hear.

RumorsPostfailure:  [1006]
Hear about the gang that ambushed that %ra? Funny business that, if you ask me.
<--->
Those guys who ambushed that %ra weren't from around here. Hired assassins.

RumorsPostsuccess:  [1007]
Hear some mystery man is hiring assassins to kill a certain %ra mercenary.
<--->
_contact_ was mad as can be. You'd think %g's be happy to see %g2 cousin.

QuestLogEntry:  [1010]
%qdt:
 Princess Elysana has asked me to escort
 her cousin _foil_ to ___contact_
 of ____contact_ to meet _contact_. %g will pay
 me =reward_ gold, and give me something
 relative to the Totem.

Message:  1011
<ce>                    _foil_ suddenly runs away, %g2
<ce>               feet making no sound as %g disappears into
<ce>                              the shadows.

Message:  1012
<ce>                       %pcf, it wasn't my fault!
<ce>                 I was hired to set you up! Here, take
<ce>                 the _reward_ gold. Some guy in a gray
<ce>                   cloak paid me exactly that much to
<ce>                      do it. _foil_ was just some
<ce>                  whore paid to go with you. You know,
<ce>                 I just remembered. I have _bribe_ gold
<ce>                   pieces that are just too heavy to
<ce>                 carry anymore. Why don't you take them
<ce>                        and I'll just be going.

Message:  1013
Kill %pcn before too much is
 learned. Use any means.
<ce>                                    
<ce>                                 -- W.

Message:  1015
<ce>                   A street urchin races up calling
<ce>                         "Message for %pcn from
<ce>                  the man in gray." The child runs off
<ce>                  before you can grab him, leaving you
<ce>                            holding a note.

Message:  1016
%pcn,
 
 My good friend Lord Woodborne says
 you are a hero that can be trusted.
 I would be truly indebted to you if
 you would stop by some time to chat.
 Maybe you could even do something
 for me. I would be oh so grateful.
 
<ce>                            Princess Elysana
<ce>                             Castle Wayrest

Message:  1018
%pcf,
 
 If you don't want to be dead as
 Lysandus, stop your investigations
 immediately. Do you really want to
 risk your life for a dead man?
 You are being watched.

Message:  1020
<ce>                      _foil_ says "Who were those
<ce>                  barbarians? They almost killed you!
<ce>                   Maybe you should just forget about
<ce>                           Lysandus' ghost."

Message:  1021
<ce>                       _foil_ says "You're much
<ce>                          tougher than Elysana
<ce>                          said you would be."


-- Symbols used in the QRC file:
--
--                %g occurs 7 times.
--               %g2 occurs 1 time.
--               %g3 occurs 65 times.
--              %pcf occurs 4 times.
--              %pcn occurs 3 times.
--              %qdt occurs 1 time.
--               %ra occurs 4 times.
--         =contact_ occurs 1 time.
--          =reward_ occurs 2 times.
--      ____contact_ occurs 5 times.
--           _bribe_ occurs 1 time.
--         _contact_ occurs 4 times.
--            _foil_ occurs 9 times.
--          _reward_ occurs 1 time.

QBN:
Item _note_ letter used 1013
Item _reward_ gold
Item _letter45_ letter used 1018
Item _letter46_ letter used 1016
Item _bribe_ gold

Person _elysana_ named Princess_Elysana atHome
Person _foil_ face 115 faction The_Prostitutes female remote
Person _contact_ group Local_3.3 remote
Person Woodborne face 1 named Lord_Woodborne

Clock _giveLetter_ 7.00:00 14.00:00
Clock _S.14_ 2.00:00 0 flag 1 range 0 1

Foe _barbarians_ is Barbarian
Foe _knights_ is Knight

--	Quest start-up:
	start timer _giveLetter_ 
	give pc _letter45_ notify 1015 
	add _elysana_ as questor 
	start task _spawnBarbarians_
	start task _spawnKnights_

_npcclicked_ task:
	clicked npc _contact_ 
	start timer _S.14_ 
	hide npc _contact_ 

_S.01_ task:
	when not _foilGone_ and _npcclicked_ 
	give pc _reward_ 
	drop _foil_ face 

_S.02_ task:
	when _foilGone_ and _npcclicked_ 
	get item _reward_ 
	get item _bribe_ 
	say 1012 
	drop _foil_ face 

_yes_ task:
	log 1010 step 0 
	say AcceptQuest 
	add _foil_ face 
	change repute with _elysana_ by -25 
	unset _clickedElysana_ 

_no_ task:
	say RefuseQuest 
	end quest 

_S.05_ task:
	injured _barbarians_ 
	give item _note_ to _barbarians_ 

_S.06_ task:
	injured _knights_ 
	give item _letter45_ to _knights_ 

_S.07_ task:
	killed 3 _barbarians_ 
	unset _spawnBarbarians_

_slainBarbarians_ task:
	when _S.07_ and _yes_
	say 1020 
	
_spawnBarbarians_ task:
	send _barbarians_ every 70 minutes 10 times with 10% success 

_S.08_ task:
	killed 2 _knights_ 
	unset _spawnKnights_

_slainKnights_ task:
	when _S.08_ and _yes_
	say 1021 

_spawnKnights_ task:
	send _knights_ every 91 minutes 10 times with 10% success 

_foilGone_ task:
	when _slainKnights_ and _slainBarbarians_ and not _daytime_ 
	say 1011 
	drop _foil_ face 

_daytime_ task:
	daily from 06:00 to 18:00 

_giveLetter_ task:
	give pc _letter46_ silently 

_clickedElysana_ task:
	clicked npc _elysana_ 

_S.13_ task:
	when _clickedElysana_ and _giveLetter_ 
	prompt QuestorOffer yes _yes_ no _no_ 

_clearclick_ task:
	when _clickedElysana_ and not _giveLetter_
	clear _clickedElysana_ _clearclick_ 

_S.14_ task:
	end quest 

FinishedMantellanCrux _S.15_
	end quest 
