-- Quest: C:\QUESTS\WORKING\20C00Y00.Qbn.
-- StartsBy: NPC
-- Questee: anyone
-- Questor: Molag Bal
-- Repute: 0
-- QuestId: 0
Messages: 61
Quest: 20C00Y00
DisplayName: Molag Bal's Quest
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>             So you want power, %pcn. Power is a far more
<ce>          tangible quantity than wisdom, don't you agree? Now
<ce>          then, in order for us to judge your worth, we have a
<ce>         bit of toil for you to undertake. Nothing unpleasant,
<ce>              of course. You may rather enjoy it. A simple
<ce>           assassination, for which you will be rewarded with
<ce>              _artifact_. Then we can see if you're ready
<ce>               for real power. Does this sound agreeable?

RefuseQuest:  [1001]
<ce>             You have wasted our time. We are not pleased.

AcceptQuest:  [1002]
<ce>            We thought it would appeal to you. Now, listen
<ce>              carefully: your target is a _monster_ named
<ce>                =monster_. This infernal sparkcaster has
<ce>         been invoking our name, drawing on our power, without
<ce>          a contract. Perhaps this is due to ignorance, but we
<ce>              cannot be merciful. Look for the mage in %g3
<ce>                laboratory, ___mondung_ and destroy %g2.
<ce>          Then, before =1stparton_ days have passed, meet our
<ce>                   agent, _contact_, a =contact_, in
<ce>                 __tavern_ at a place called _tavern_.
<ce>            There %g will give you _artifact_. Do not tarry,
<ce>            time is short. We will be watching. Do not fail.

QuestFail:  [1003]
<ce>                Listen, %pcf, you and I both know that
<ce>               =monster_'s still breathing. A contract is
<ce>                a contract, and Molag Bal never forgets.

QuestComplete:  [1004]
<ce>             Hello, %pcf. You made it. Molag Bal sends his
<ce>                compliments. And, of course, _artifact_.
<ce>                      Enjoy it and fare thee well.

RumorsDuringQuest:  [1005]
Everyone's whispering about the experiments going on in ___mondung_.
<--->
There's some weird stuff going on in that ___mondung_ to the %di.

RumorsPostfailure:  [1006]
That bright red star is said to be the anger of the daedra Molag Bal.
<--->
There was a crazy =contact_ running around here screaming about contracts.

RumorsPostsuccess:  [1007]
Real quiet around ___mondung_ lately.
<--->
There was a lot of excitement around ___mondung_, but it's all calmed down.

Message:  1011
A regal Daedra Prince, said to be the most power-mad of all daedra.
<--->
Molag Bal is said to be obsessed with making Tamriel part of his realm.

Message:  1012
Some weird-acting =contact_ was in __tavern_, %di of here.
<--->
A =contact_ by that name was in __tavern_, %di of here, fairly recently.

Message:  1013
The Mace of Molag Bal steals magicka from its victims.
<--->
<ce>The Mace of Molag Bal, or the Vampire's Mace to the layman, is a conduit
<ce>              for magical energy to flow into the bearer.

Message:  1020
%qdt:
 The Daedra Prince Molag Bal has sent me to
 kill a spellcaster of some kind in dungeon
 ___mondung_. If the mage is dead
 within =1stparton_ days and I meet with his
 associate _contact_ in __tavern_
 at a place called _tavern_,
 I will be given _artifact_.

Message:  1030
<ce>                          =monster_ is dead.
<ce>                           You have fulfilled
<ce>                           your contract with
<ce>                           Molag Bal.  Now to
<ce>                          collect your reward.

Message:  1040
<ce>                       =monster_ lifts %g3 staff
<ce>                    and disappears in a thunderclap.

Message:  1060
<ce>              A map showing the location of ___hideout_,
<ce>                       marked with the notation,
<ce>                  "Remind _dummy_ to lay in supplies.
<ce>              Indications that Molag Bal has detected me."


-- Symbols used in the QRC file:
--
--               %di occurs 3 times.
--                %g occurs 1 time.
--               %g2 occurs 1 time.
--               %g3 occurs 2 times.
--              %pcf occurs 2 times.
--              %pcn occurs 1 time.
--              %qdt occurs 1 time.
--       =1stparton_ occurs 2 times.
--         =contact_ occurs 4 times.
--         =monster_ occurs 4 times.
--       ___hideout_ occurs 2 times.
--       ___mondung_ occurs 6 times.
--         __tavern_ occurs 4 times.
--        _artifact_ occurs 5 times.
--         _contact_ occurs 2 times.
--           _dummy_ occurs 1 time.
--         _monster_ occurs 1 time.
--          _tavern_ occurs 2 times.

QBN:
Item _artifact_ artifact Mace_of_Molag_Bal anyInfo 1013
Item _map_ letter used 1060

Person _questgiver_ face 112 named Molag_Bal anyInfo 1011
Person _contact_ face 233 faction The_Cabal remote anyInfo 1012
Person _dummy_ face 1 group Spellcaster remote

Place _mondung_ remote dungeon9
Place _tavern_ remote tavern
Place _hideout_ remote dungeon11

Clock _1stparton_ 00:00 0 flag 1 range 0 5
Clock _S.08_ 00:01 00:04

Foe _monster_ is Mage
Foe _monster1_ is Mage

--	Quest start-up:
	place item _map_ at _mondung_ 
	start timer _1stparton_ 
	reveal _mondung_ 
	log 1020 step 0 
	_map_ used do _readmap_ 
	place foe _monster_ at _mondung_ 
	place npc _contact_ at _tavern_ 

_pcgetsgold_ task:
	when _qgclicked_ and _mondead_ 
	give pc _artifact_ 
	end quest 
--removed unnecessary delay

variable _queston_
--unused variable
_mondead_ task:
	killed 1 _monster_
	killed 1 _monster1_ 
	say 1030 
	--added kill condition for monster1

_qgclicked_ task:
	clicked npc _contact_ 

_1stparton_ task:
	end quest 

_clearclick_ task:
	when _qgclicked_ and not _mondead_ 
	--this used the timer 2ndparton erroneously. properly changed to mondead.
	say 1003
	clear _qgclicked_ _clearclick_ 

variable _S.08_
_S.09_ task:
	injured _monster_ 
	start timer _S.08_ 

_S.10_ task:
	when _S.08_ and not _mondead_
	remove foe _monster_
	place foe _monster1_ at _hideout_
	say 1040
--saying 1040 was previously used here as an invalid argument. Moved it to its own line.
--added new monster1 creature and properly enabled disappearing act with new remove action.

_readmap_ task:
	reveal _hideout_ readmap
