-- Quest: C:\QUESTS\WORKING\B0C00Y05.Qbn.
-- StartsBy: NPC
-- Questee: anyone
-- Questor: knight
-- Repute: 0
-- QuestId: 5
-- Edited for Daggerfall Unity by Jay_H
Messages: 81
Quest: B0C00Y05
DisplayName: The Traitor
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                  You are not yet a trusted member of
<ce>                       our knightly order, %pcn.
<ce>                   However, I think I know a way you
<ce>                     can earn our trust. There is a
<ce>                  traitor to the order in ___qgiver_,
<ce>                     a dastardly _monster_ who has
<ce>                   betrayed us to our enemies. Death
<ce>                   is a fair punishment for treason,
<ce>                       don't you think %pcn? Will
<ce>                       you do this for the order?

RefuseQuest:  [1001]
<ce>                     Don't expect any favors from
<ce>                   this knightly order anytime soon.

AcceptQuest:  [1002]
<ce>                      The traitor is a _monster_
<ce>                     named =monster_ who is hiding
<ce>                        like a craven coward in
<ce>                              ___mondun_.
<ce>                        If you have not returned
<ce>                    in =queston_ days, our trust is
<ce>                     forfeit and another will take
<ce>                              your place.

QuestFail:  [1003]
<ce>                 The _monster_ is not dead. Do not try
<ce>                       to convince me otherwise.

QuestComplete:  [1004]
<ce>                       Maybe you are not as bad
<ce>                         as they say. %pcn, you
<ce>                      have earned a bit of respect
<ce>                       with this knightly order.

RumorsDuringQuest:  [1005]
That _monster_ ran off to ___mondun_ like the hounds of hell were behind him.
<--->
_qgiver_ can't just let =monster_ go after what %g did to the knightly order.

RumorsPostfailure:  [1006]
=monster_ is apparently tougher than _qgiver_ thought. What an escape.
<--->
=monster_ has moved north I hear, but %g3 memory still plagues the order.

RumorsPostsuccess:  [1007]
=monster_'s story just proves it. You don't play around with the ==qgiver_.
<--->
I always thought the Order would be more merciful to a dolt like =monster_.

QuestorPostsuccess:  [1008]
I'd be happy to help you however I can, killer.

QuestorPostfailure:  [1009]
Your heart is too soft, %pcf. Don't waste my time.

QuestLogEntry:  [1010]
%qdt:
 _qgiver_ of ___qgiver_
 has sent me to find and slay a _monster_
 called =monster_ who is slinking
 around in ___mondun_. I have
 =queston_ days to accomplish this.

Message:  1011
<ce>               You have killed =monster_, the _monster_.
<ce>                   The knightly order will be proud.

Message:  1080
<ce>                    As a token of our appreciation,
<ce>                 please accept this map to ___newdung_,
<ce>                  a foul, vermin-infested hole by all
<ce>                   accounts.  Just the kind of place
<ce>                     a budding knight should enjoy
<ce>                              cleaning up.


-- Symbols used in the QRC file:
--
--                %g occurs 1 time.
--               %g3 occurs 1 time.
--              %pcf occurs 2 times.
--              %pcn occurs 3 times.
--              %qdt occurs 1 time.
--         ==qgiver_ occurs 1 time.
--         =monster_ occurs 28 times.
--         =queston_ occurs 2 times.
--        ___mondun_ occurs 3 times.
--       ___newdung_ occurs 1 time.
--        ___qgiver_ occurs 2 times.
--         _monster_ occurs 6 times.
--          _qgiver_ occurs 3 times.

QBN:

Person _qgiver_ group Questor

Place _mondun_ remote dungeon
Place _newdung_ remote dungeon

Clock _queston_ 00:00 0 flag 17 range 0 2

Foe _monster_ is Nightblade

--	Quest start-up:
	start timer _queston_ 
	reveal _mondun_ 
	log 1010 step 0 
	pick one of _map_ _nomap_ _nomap_ 
	place foe _monster_ at _mondun_ 

_queston_ task:
	end quest 

_pcgetsgold_ task:
	when _mondead_ and _qgclicked_ 
	give pc nothing 

_mondead_ task:
	killed 1 _monster_ saying 1011 

_qgclicked_ task:
	clicked npc _qgiver_ 

_clearclick_ task:
	when not _mondead_ and _qgclicked_ 
	say 1003
	clear _qgclicked_ _clearclick_ 

variable _map_
variable _nomap_
_S.07_ task:
	when _map_ and _pcgetsgold_
    say 1080
	reveal _newdung_
	end quest

_S.08_ task:
    when not _map_ and _pcgetsgold_
    end quest
