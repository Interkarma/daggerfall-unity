Quest: S0000503
DisplayName: Former Student Part III
QRC:

Message:  1020
%qdt:
 I have received a mysterious summons
 to _tavern_ in __tavern_
 in ____tavern_, I believe by my friend _rebel_.

Message:  1021
%qdt:
 I have agreed to help _rebel_
 "liberate" a map to ___itemdung_
 from the Mages Guild of __magesguild_.

Message:  1022
%qdt:
 We have retrieved the map.  _rebel_
 has another matter to attend to, so
 I will meet him back in _tavern_
 in =keytime_ days.

Message:  1023
%qdt:
 We have retrieved the map.  I have
 offered to go to ___keydung_
 to find a special _holydagger_ which we
 need to enter ___itemdung_.
 I will meet _rebel_ back in
 _tavern_ in =keytime_ days.

Message:  1030
<ce>                    A tattered map to ___itemdung_.

Message:  1040
<ce>               It was good of you to come.  I have a new
<ce>             project that I think you might be interested
<ce>               in.  It will involve a little raid on our
<ce>                 friends at the Mages Guild, however.
<ce>                           What do you say?

Message:  1041
<ce>                Have it your way.  I thought you and I
<ce>                       were friends, that's all.

Message:  1042
<ce>             I knew you couldn't resist!  Here's the plan.
<ce>             For some time now, I have been searching for
<ce>                the location of ___itemdung_, reputedly
<ce>           the resting place of a hoard of magical objects,
<ce>              including one _magicreward0_ that I need for my own
<ce>              purposes -- another story for another time.
<ce>             The issue at hand is that I have learned that
<ce>               the Mages Guild of __magesguild_ has come
<ce>               into possession of a map to ___itemdung_.
<ce>           I still have a few friends in the Guild, despite
<ce>             the official ban against me... The Guild has
<ce>             no idea what they have, so it is up to us to
<ce>              liberate the map so that it can be used to
<ce>                          its full potential.

Message:  1043
<ce>                         Yes, this is the map!

Message:  1044
<ce>                       _rebel_ raises his staff.
<ce>           "Ladies, gentlemen, we really must be going now."
<ce>               A sphere of light bursts from the staff,
<ce>                     blinding you for an instant.

QuestTimeLapse:  [1045]
<ce>           I think it would be best to separate for the time
<ce>           being, to let this hornet's nest calm down a bit.
<ce>           I have one more matter to attend to before we can
<ce>                   enter ___itemdung_ ... unless you
<ce>                    would like to handle it for me?

Message:  1046
<ce>          Very well.  ___itemdung_ cannot be entered without
<ce>          the key, a _holydagger_ which I have learned is somewhere
<ce>           in ___keydung_.  Once you get the _holydagger_, meet me
<ce>           in _tavern_ in __tavern_, say, in =keytime_ days.

Message:  1047
<ce>            No matter.  I will need, say, =keytime_ days to
<ce>             take care of this business.  I will meet you
<ce>                    back in __tavern_, at _tavern_,
<ce>                    when I have acquired the key to
<ce>                       ___itemdung_.  Good luck!

Message:  1050
<ce>           This must be the _holydagger_, the key to ___itemdung_.

Message:  1051
<ce>          With this _holydagger_ in our possession, we are ready to
<ce>               brave ___itemdung_.  Lead on, my friend!

Message:  1052
<ce>                         A hollow voice booms,
<ce>                   "Intruders are not welcome here!"

Message:  1053
<ce>              You are too late, _rebel_!  It was a simple
<ce>              matter to follow you to ___itemdung_, once
<ce>              you had found the _holydagger_ for me.  The _magicreward0_
<ce>                     is now mine, and mine alone!

Message:  1054
<ce>             "Well, friend %pcf, an enjoyable little jaunt
<ce>             eh?  I'll take the _magicreward0_ and be on my way."
<ce>                                    
<ce>                  Do you give the _magicreward0_ to _rebel_?

Message:  1055
<ce>                           Until next time!

Message:  1056
<ce>                     So, your true colors at last.
<ce>                   I only asked for one small thing,
<ce>                       yet your greed apparently
<ce>                           knows no bounds.

Message:  1070
<ce>                   A shadow flits overhead.  A raven
<ce>                  circles, cawing out harshly before
<ce>                      disappearing into the west:
<ce>                                    
<ce>                          "No time!  No time!
<ce>                               _tavern_,
<ce>                               __tavern_
<ce>                             ____tavern_!"

Message:  1071
<ce>                          %pcf, are you lost?
<ce>                         I'm afraid I'll have
<ce>                           to leave you now.

QBN:
Item _letter_ letter
Item _map_ letter used 1030
Item _magicreward0_ magic_item
Item _magicreward1_ magic_item
Item _magicreward2_ magic_item
Item _goldreward_ gold
Item _holydagger_ item class 10 subclass 11

Person _rebel_ face 1 named Baltham_Greyman
Person _dummy_ face 249 group Spellcaster remote

Place _tavern_ remote tavern
Place _magesguild_ remote magery
Place _itemdung_ remote dungeon
Place _keydung_ remote dungeon
Place _teleportdungeon_ remote dungeon

Clock _giveletter_ 62.00:00 97.00:00
-- I temporarily set this to 1 day for debug purposes and forgot to return it.
-- 62 to 97 is the correct timer
Clock _30days_ 30.00:00
-- they had to intend 30 specific days for some reason I don't know
Clock _firstbalthamtimer_ 00:00 0 flag 1 range 0 2
Clock _stuntimer_ 00:01
Clock _keytime_ 00:00 0 flag 1 range 0 5
Clock _sixdays_ 6.22:40 0 flag 1 range 0 1
Clock _secondbalthamtimer_ 00:00 0 flag 17 range 0 2
Clock _deathtimer_ 0:10

Foe _guards_ is 3 Mage
Foe _guardian_ is Battle_mage
Foe _balthamenemy_ is Mage

--	Quest start-up:
	start timer _giveletter_ 
	give item _goldreward_ to _guardian_ 
	give item _magicreward2_ to _guardian_ 
	give item _magicreward1_ to _guardian_ 
	give item _magicreward0_ to _guardian_ 
	pc at _magesguild_ set _inmagery_ 
	pc at _itemdung_ set _indungeon_ 
	_map_ used do _readmap_ 
	place foe _guardian_ at _itemdung_ 
	have _magicreward0_ set _takemageitem_

_giveletter_ task:
	place item _map_ at _magesguild_ 
	start timer _30days_ 
	have _letter_ set _haveletter_ 
	give pc _letter_ notify 1070 

_haveletter_ task:
	take _letter_ from pc 
	log 1020 step 0 
	place npc _rebel_ at _tavern_ 
	restore _rebel_

variable _30days_
_clickbaltham_ task:
	clicked npc _rebel_ 
	start task _firstprompt_ 

_firstprompt_ task:
	prompt 1040 yes _acceptquest_ no _target_ 

_atimerhasended_ task:
	when _sixdays_ and not _journeytodungeon_ 
	when _30days_ and not _acceptquest_ 

_acceptquest_ task:
	start timer _firstbalthamtimer_ 
	log 1021 step 1 
	say 1042 
	add _rebel_ face 
	hide npc _rebel_
	stop timer _30days_

_target_ task:
	say 1041 
	remove log step 0
--log message would stay in journal without this

variable _firstbalthamtimer_

_clickmap_ task:
	clicked item _map_ 
	start timer _stuntimer_ 
	say 1043 

_spawnguards_ task:
	when _clickmap_ and _inmagery_
	create foe _guards_ every 0 minutes 1 times with 100% success 
-- ensures guards can only spawn inside the guild

_stuntimer_ task:
	say 1044 
	restrain foe _guards_ 

variable _inmagery_

_offersidequest_ task:
	when _clickmap_ and not _inmagery_ 
	start timer _keytime_ 
	prompt 1045 yes _acceptsidequest_ no _rejectsidequest_ 
	drop _rebel_ face 
	change repute with _dummy_ by -15 
	stop timer _firstbalthamtimer_
	stop timer _stuntimer_
--added stop stuntimer here, otherwise it could trigger when irrelevant

_keytime_ task:
	start timer _sixdays_ 
	place npc _rebel_ at _tavern_
	restore _rebel_

_acceptsidequest_ task:
	place item _holydagger_ at _keydung_ 
	reveal _keydung_ 
	log 1023 step 2 
	say 1046 
	restore _rebel_

_rejectsidequest_ task:
	log 1022 step 2 
	say 1047 
	clear _clickbaltham_

_clickdagger_ task:
	clicked item _holydagger_ 
	say 1050 
	clear _clickbaltham_

_indungeon_ task:
	have _holydagger_ set _havedagger_ 

_journeytodungeon_ task:
	when _clickbaltham_ and _clickdagger_ and _acceptsidequest_ 
	when _rejectsidequest_ and _clickbaltham_ 
	start timer _secondbalthamtimer_
	stop timer _keytime_ 
	say 1051 
	hide npc _rebel_ 
	add _rebel_ face 
--there's still a lingering bug here that I can't figure out. If you do the S.14 route, Baltham's sprite will remain visible at this point. I don't know why.

variable _sixdays_
_timeisup_ task:
	when _secondbalthamtimer_ and not _finaldecision_ 
	when _firstbalthamtimer_ and not _offersidequest_ 
	say 1071 

_readmap_ task:
	reveal _itemdung_ 

_teleporttrap_ task:
	when _indungeon_ and not _havedagger_ 
	reveal _teleportdungeon_ 
	say 1052 
	teleport pc to _teleportdungeon_ 
	unset _teleporttrap_ 

variable _havedagger_
_hitguardian_ task:
	injured _guardian_ 
	say 1053 

_removefirstreward_ task:
	take _magicreward0_ from pc 
	say 1055 

_defendfirstreward_ task:
	create foe _balthamenemy_ every 0 minutes 1 times with 100% success 
	say 1056 
	make _magicreward0_ permanent 

variable _takemageitem_

_finaldecision_ task:
	when _takemageitem_ and not _indungeon_ 
	prompt 1054 yes _removefirstreward_ no _defendfirstreward_ 
	drop _rebel_ face 
	remove log step 0
	remove log step 1
	remove log step 2
-- needed to add remove-log actions due to new deathtimer task, see below

variable _secondbalthamtimer_

_finalend_ task:
	when _atimerhasended_ or _timeisup_ or _secondbalthamtimer_ or _removefirstreward_ 
	make _goldreward_ permanent 
	make _magicreward2_ permanent 
	make _magicreward1_ permanent 
	end quest 

_afreedagger_ task:
	when _journeytodungeon_ and _rejectsidequest_ 
	get item _holydagger_ 

_endofbaltham_ task:
	killed _balthamenemy_
	make _goldreward_ permanent 
	make _magicreward2_ permanent 
	make _magicreward1_ permanent 
	start timer _deathtimer_

_deathtimer_ task:
	end quest
-- this is necessary due to a change near DFU 1.0 to make corpses disappear at quest end.
-- this delay allows player to loot corpse.