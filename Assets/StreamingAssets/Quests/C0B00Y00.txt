-- Quest: C:\QUESTS\WORKING\C0B00Y00.Qbn.
-- StartsBy: NPC
-- Questee: member
-- Questor: temple
-- Repute: 0
-- QuestId: 0
-- Edited for Daggerfall Unity by Jay_H
Messages: 13
Quest: C0B00Y00
DisplayName: The Haunted House
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                       A house over in __house_
<ce>            is being haunted by some sort of undead. We need
<ce>           someone to exorcise the spirit. Are you up to it?
<ce>                      It pays _gold_ gold pieces.

RefuseQuest:  [1001]
<ce>           No shame in that. I'll keep an eye open for jobs
<ce>                          more to your liking.

AcceptQuest:  [1002]
<ce>            Remember, if the haunt is too tough for you to
<ce>          handle yourself, just get out of there. No one will
<ce>          blame you for saving your skin. The place is called
<ce>                     _house_ and it's in __house_.
<ce>           You have =traveltime_ days or we send someone else
<ce>                           out to do the job.

QuestComplete:  [1004]
<ce>          So the haunt is no more. I will burn incense at the
<ce>             altar for its spirit. Here is your temple fee.

RumorsDuringQuest:  [1005]
The ghost of _house_ cannot be killed, they say.

RumorsPostfailure:  [1006]
The ghost of _house_ will keep coming back.
<--->
The spirits of _house_ are still not at rest.

RumorsPostsuccess:  [1007]
_house_ is clean at last.
<--->
The spirits of _house_ are at last at rest.

QuestorPostsuccess:  [1008]
How may I help you, o exorciser of _house_?

QuestorPostfailure:  [1009]
_house_ is still haunted, and I should speak with thee?

QuestLogEntry:  [1010]
%qdt:
 _qgiver_ of __qgiver_
 in ___qgiver_
 sent me on an errand to rid
 _house_ in __house_
 of an undead haunt. I have
 =traveltime_ days to do the job.

Message:  1011
<ce>          I am the ancestor of the builder of this house. It
<ce>           now belongs to the dead. Let all living creatures
<ce>                            shun this place.

Message:  1012
<ce>          I can see why you didn't stick around to fight that
<ce>                thing, %pcn. You have the thanks of the
<ce>            temple for at least finding out about the haunt.
<ce>                          Here's 2 gold coins.


-- Symbols used in the QRC file:
--
--              %pcn occurs 1 time.
--              %qdt occurs 1 time.
--      =traveltime_ occurs 2 times.
--        ___qgiver_ occurs 1 time.
--          __house_ occurs 3 times.
--         __qgiver_ occurs 1 time.
--            _gold_ occurs 2 times.
--           _house_ occurs 9 times.
--          _qgiver_ occurs 1 time.

QBN:
Item _gold_ gold
Item boobyprize gold range 2 to 2

Person _qgiver_ face 1 group Questor

Place _house_ remote house2

Clock _traveltime_ 00:00 0 flag 17 range 0 2

Foe _ghost_ is Ghost
Foe _skeleton_ is Skeletal_warrior
Foe _wraith_ is Wraith
Foe _zombie_ is Zombie

--	Quest start-up:
	start timer _traveltime_ 
	log 1010 step 0 
	pick one of _S.02_ _S.03_ _S.04_ _S.05_ 
	pc at _house_ set _S.08_ 
	create npc at _house_ 

_traveltime_ task:
	end quest 

_questdone_ task:
	clicked npc _qgiver_ 

_success_ task:
	when _questdone_ and _S.06_ 
	give pc _gold_ 
	end quest 

_S.02_ task:
	place foe _ghost_ at _house_ 

_S.03_ task:
	place foe _skeleton_ at _house_ 

_S.04_ task:
	place foe _wraith_ at _house_ 

_S.05_ task:
	place foe _zombie_ at _house_ 

_S.06_ task:
	killed 1 _zombie_ 
	killed 1 _skeleton_ 
	killed 1 _wraith_ 
	killed 1 _ghost_ 

_S.07_ task:
	injured _zombie_ saying 1011 
	injured _wraith_ saying 1011 
	injured _skeleton_ saying 1011 
	injured _ghost_ saying 1011 

variable _S.08_
variable _S.09_
_clearclick_ task:
	when _questdone_ and not _S.06_ 
	clear _questdone_ _clearclick_ 
