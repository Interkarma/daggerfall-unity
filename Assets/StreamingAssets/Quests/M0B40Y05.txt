-- Quest: C:\QUESTS\WORKING\M0B40Y05.Qbn.
-- StartsBy: NPC
-- Questee: member
-- Questor: fighter
-- Repute: 40
-- QuestId: 5
-- Edited for Daggerfall Unity by Jay_H
Messages: 81
Quest: M0B40Y05
DisplayName: Hunt for a Spriggan
-- Message panels
QRC:

QuestorOffer:  [1000]
<ce>                     Some woodsmen have hired the
<ce>                       guild to kill a spriggan.
<ce>                    Seems the monster is attacking
<ce>                     them when they are out in the
<ce>                     woods logging. Want to take a
<ce>                           hack at it %pcn?

RefuseQuest:  [1001]
<ce>                     Fair enough. Run along. I've
<ce>                      got to find someone to take
<ce>                               this job.

AcceptQuest:  [1002]
<ce>                      Two attempted ambushes have
<ce>                     failed. The spriggan is just
<ce>                     too smart. You'll have to go
<ce>                     to its lair, in ___dungeon_.
<ce>                     Get back to me within =2dung_
<ce>                    days and I'll have _gold_ gold
<ce>                           waiting for you.

QuestFail:  [1003]
<ce>                       The spriggan is kindling?
<ce>                      You are proving yourself to
<ce>                       be quite reliable, %pct.

QuestComplete:  [1004]
<ce>                       Here is your _gold_ gold
<ce>                             as promised.

RumorsDuringQuest:  [1005]
<ce>                     I hear _woodsman_ is stirring
<ce>                up the woodsmen against the spriggans.
<ce>                 Funny. I've never known the spriggans
<ce>                      to cause us trouble before.

RumorsPostfailure:  [1006]
<ce>                   Another woodsman died. I hear it
<ce>                      was _woodsman_. The rest of
<ce>                  them have decided to log elsewhere.

RumorsPostsuccess:  [1007]
<ce>                  I haven't heard about that spriggan
<ce>                   lately. Must have left the area.

QuestorPostsuccess:  [1008]
<ce>                  Well done work with that spriggan.

QuestorPostfailure:  [1009]
<ce>                    Spriggans aren't so tough. What
<ce>                   happened? You get scared and hid?

QuestLogEntry:  [1010]
On %qdt,
 The Fighters Guild of ___qgiver_
 hired me to kill a spriggan
 that is molesting woodsmen.
 It's hiding in ___dungeon_.
 I was given =2dung_ days to
 get the job done.

Message:  1011
<ce>                         The spriggan is dead.
<ce>                       Those woodsmen can log in
<ce>                              safety now.

Message:  1020
<ce>                     You attack me without cause,
<ce>                         %ra. Yes I did wreak
<ce>                     vengeance upon the slayers of
<ce>                     wood. Know that they did slay
<ce>                     seedlings of mine. Would you
<ce>                    stand rooted while someone slew
<ce>                   your seedlings? I have no quarrel
<ce>                    with you. Convince the woodsmen
<ce>                    to prune with care, and I shall
<ce>                     leave them in peace. Will you
<ce>                         take this mission up?

Message:  1021
<ce>                    The main culprit is _woodsman_,
<ce>                  who hacks down trees without care. Go to
<ce>                    __woodsman_ in  ___woodsman_
<ce>                   and secure %g3 promise. Do not visit me again. I
<ce>                 shall know of your success or failure
<ce>                  by how the woodsmen do their work.

Message:  1022
<ce>                  I should have known better than to
<ce>                   trust a creature of flesh. Now I
<ce>                      shall give you no quarter.

Message:  1023
<ce>                   So the tree creature wants to cut
<ce>                   a deal, eh? Well I suppose I can
<ce>                   convince the boys to only log the
<ce>                   big trees. But if his kind attack
<ce>                  us again, we'll burn every sapling
<ce>                  we can find! You've got yourself a
<ce>                              deal, %pcn.

Message:  1025
<ce>                   You worked it so the woodsmen and
<ce>                   the spriggan ain't fightin'? Very
<ce>                   clever %pcn. You'll go far if you
<ce>                        keep those wits sharp.

Message:  1030
%qdt:
 The spriggan has convinced me that
 it is not the villain. Apparently a
 woodsman named _woodsman_
 in __woodsman_, ___woodsman_
 has been cutting down its trees. If I can get
 _woodsman_ to agree to log
 the forest more carefully, this whole
 thing can be settled without bloodshed.

Message:  1080
<ce>               By the way, since I'm feelin' charitable,
<ce>              here's a map to a hole called ___newdung_.
<ce>              Full of monsters needin' killing, treasure
<ce>                    to loot, the works.  Have fun.


-- Symbols used in the QRC file:
--
--              %pcn occurs 5 times.
--              %pct occurs 2 times.
--              %qdt occurs 2 times.
--               %ra occurs 1 time.
--           =2dung_ occurs 2 times.
--       ___dungeon_ occurs 2 times.
--         ___house_ occurs 2 times.
--       ___newdung_ occurs 1 time.
--        ___qgiver_ occurs 1 time.
--            _gold_ occurs 4 times.
--        _woodsman_ occurs 5 times.

QBN:
Item _gold_ gold

Person _qgiver_ group Questor
Person _woodsman_ face 168 group Resident2 remote

Place _dungeon_ remote dungeon
Place _newdung_ remote dungeon

Clock _2dung_ 00:00 0 flag 17 range 0 2
Clock _end_ 00:00

Foe _monster_ is Spriggan

--	Quest start-up:
	start timer _2dung_ 
	reveal _dungeon_ 
	log 1010 step 0 
	pick one of _map_ _nomap_
	place foe _monster_ at _dungeon_ 

_2dung_ task:
	end quest 

_S.01_ task:
	killed 1 _monster_ saying 1011 
	hide npc _woodsman_
	remove log step 1

_qgclicked_ task:
	clicked npc _qgiver_ 

_normalvict_ task:
    when _qgclicked_ and _S.01_
	say 1003
    start task _victory_
--1003 appears to be a victory statement for kill path

_victory_ task:
	give pc _gold_ 
    start timer _end_
--	end quest 
--this would cause double quest ending, for double reputation gain

_clearclick_ task:
	when _qgclicked_ and not _S.01_ and not _S.07_ 
	clear _qgclicked_ _clearclick_ 

_S.04_ task:
	injured _monster_

_injtalk_ task:
    when _S.04_ and not _S.01_
	prompt 1020 yes _yes_ no _no_ 

_yes_ task:
	add dialog for person _woodsman_ 
	log 1030 step 1 
	say 1021 
	restrain foe _monster_

_no_ task:
	say 1022 

_S.07_ task:
	clicked npc _woodsman_

_notkill_ task:
	when _S.07_ and _injtalk_ and not _S.01_
    say 1023 
	change repute with _woodsman_ by +10 

_S.08_ task:
	when _qgclicked_ and _notkill_ 
	say 1025 
	start task _victory_

variable _map_
variable _nomap_
_S.11_ task:
	when _map_ and _victory_ 
	reveal _newdung_ readmap
    say 1080 

_end_ task:
    end quest